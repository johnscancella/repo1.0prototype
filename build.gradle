subprojects {
  repositories {
    jcenter()
  }

  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'jacoco'
  apply plugin: 'findbugs'
  apply plugin: 'pmd'

  test { //show test output
    testLogging.showStandardStreams = true
  }

  jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled true
    }
  }

  jacocoTestReport.dependsOn test

  findbugsMain{
    reports{
      xml.enabled = false
      html.enabled = true
    }
  }

  findbugsTest{
    reports{
      xml.enabled = false
      html.enabled = true
    }
  }

  pmd {
    ruleSets = ["java-basic", "java-braces"]
  }
  
//https://issues.jenkins-ci.org/browse/JENKINS-9438
//cause the junit test output isn't updated the build fails, this updates the timestamp
  task jenkinsTest{
    inputs.files test.outputs.files
    doLast{
        def timestamp = System.currentTimeMillis()
        test.testResultsDir.eachFile { it.lastModified = timestamp }
    }
  }
 
  build.dependsOn(jenkinsTest)
}
