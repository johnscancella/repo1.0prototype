apply plugin: 'spring-boot'

mainClassName="gov.loc.rdc.app.MainApplication"

//when not building an offical release (i.e. versioned) add debug info for easy debugging
if(project.version == "unspecified"){
  applicationDefaultJvmArgs += ["-Xdebug", "-Xrunjdwp:server=y,transport=dt_socket,address=4000,suspend=n"]
}

startScripts{
  doLast{
    unixScript.text = addPropertiesFileFlag(unixScript, mainClassName, ' --spring.config.location=file:$APP_HOME/config/application.properties')
    windowsScript.text = addPropertiesFileFlag(windowsScript, mainClassName, ' --spring.config.location=file:%APP_HOME%/config/application.properties')
  }
}

distributions {
  main {
      contents {
        from ("application.properties"){
          into "config"
        }
      }
  }
}

public String addPropertiesFileFlag(File scriptFile, String clazz, String flagToAdd){
  StringBuilder sb = new StringBuilder()
  scriptFile.text.eachLine { String line ->
    if(line.contains(clazz)){
      sb.append(line).append(flagToAdd).append("\n")
    }else{
      sb.append(line).append("\n")
    }
  }
  
  return sb.toString()
}
