buildscript {
  repositories { jcenter() }
  dependencies { classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.5.RELEASE") }
}
plugins { id "nebula.os-package" version "2.2.6" }

apply from: '../springboot.gradle'

mainClassName="gov.loc.rdc.app.ObjectStoreApplication"

dependencies {
    compile project(':springWebAppBase'),
    'org.springframework.boot:spring-boot-starter-ws:1.2.5.RELEASE',
    'org.springframework.boot:spring-boot-starter-actuator:1.2.5.RELEASE', //adds a bunch of default endpoints to rest services
    'org.codehaus.groovy:groovy-all:2.4.0'

    testCompile 'junit:junit:4.12',
    'org.springframework.boot:spring-boot-starter-test:1.2.5.RELEASE'
}

//package as rpm or deb
ospackage {
  packageName project.name.toLowerCase() //deb requires all lowercase package names
  release '1'
  os = LINUX // only applied to RPM
  summary "A restful sha256 based object store"
  license "This software is in the Public Domain."
  url "https://github.com/loc-rdc/repo1.0prototype"
  vendor "Library of Congress"
  packager "Library of Congress"
  buildHost InetAddress.localHost.hostName
  
  into '/opt/loc-rdc/metadataStore'

  //TODO add requires here?

  from (startScripts) { into "bin" }
  from (configurations.runtime) { into "lib" }
  from (jar) { into "lib" }
  from ("application.properties") { into "config" }
}

build.dependsOn buildRpm, buildDeb
